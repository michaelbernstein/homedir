# Initialize completion subsystem
autoload -U compinit
compinit

# Easy-to-use colors for prompts
autoload colors
colors
PR_RED="%{$fg_bold[red]%}"
PR_BLUE="%{$fg_bold[blue]%}"
PR_GREEN="%{$fg_bold[green]%}"
PR_YELLOW="%{$fg_bold[yellow]%}"
PR_RESET="%{$reset_color%}"

# vcs info config
autoload vcs_info
zstyle ':vcs_info:*' enable git hg svn
zstyle ':vcs_info:*:prompt:*' formats "%s:%r:%S:%b"
zstyle ':vcs_info:*:prompt:*' actionformats "%s:%r:%S:%b:%a"

#allow tab completion in the middle of a word
setopt complete_in_word

# Don't remove slashes from tab-completed things
setopt no_auto_remove_slash

# Built-in echo is BSD compatible
setopt bsd_echo

# If the name of a dir is typed as a command, CD to that dir
setopt auto_cd

# Don't try to correct spelling of commands/args
setopt no_correct
setopt no_correct_all

# No duplicates anywhere in history
setopt hist_ignore_all_dups
setopt hist_save_no_dups

# Do a pushd every time I cd, don't tell me about it, and don't push dupes
setopt auto_pushd
setopt pushd_silent
setopt pushd_to_home
setopt pushd_ignore_dups

# Prompt and such
setopt prompt_subst

function make_prompt {
	vcs_info prompt
	rval="%n@%B%m%b"

	if [[ -n "$vcs_info_msg_0_" ]] ; then
		vtype="$(echo "$vcs_info_msg_0_" | cut -d: -f1)"
		repo="$(echo "$vcs_info_msg_0_" | cut -d: -f2)"
		rpath="/$(echo "$vcs_info_msg_0_" | cut -d: -f3 | sed -e 's/^\.//')"
		branch="$(echo "$vcs_info_msg_0_" | cut -d: -f4)"
		act="$(echo "$vcs_info_msg_0_" | cut -d: -f5)"
		if [[ $vtype = "git" ]] ; then
			vtype="$(echo -e -n \\xc2\\xb1)"
		elif [[ $vtype = "hg" ]] ; then
			vtype="$(echo -e -n \\xe2\\x98\\xbf)"
		elif [[ $vtype = "git-svn" ]] ; then
			vtype="$(echo -e -n \\xc2\\xb1)-s"
		elif [[ $vtype = "svn" ]] ; then
			vtype="s"
			branch="${branch}@${act}"
			act=""
		else
			vtype="?"
		fi
		rval="$rval $vtype $PR_GREEN$repo$PR_RESET:$PR_BLUE$branch$PR_RESET"
		if [[ -n "$act" ]] ; then
			rval="$rval $PR_RED($act)$PR_RESET"
		fi
		rval="$rval\n$rpath"
	else
		rval="$rval\n%~"
	fi

	echo -e "$rval"
}

export PROMPT="\$(make_prompt)%(!.#.>) "

# Do the smart thing when grepping, but leave plain ol' grep available if
# necessary
if type ack > /dev/null 2>&1 ; then
	alias ggrep='ack --skipped --text'
else
	ggrep() {
		if git rev-parse --show-toplevel > /dev/null 2>&1 ; then
			git grep -n --no-index -I "$@"
		else
			grep --color=yes -n -r -I "$@" | less -FRSXi
		fi
	}
fi
alias igrep='ggrep -i'

ffind() {
	find . -iname "$@"
}

if type dgit > /dev/null 2>&1 ; then
	# Use git with defaults, but make completion still work
	alias git=dgit
	compdef dgit=git
fi

# Load OS-specific zsh configuration
case "$(uname)" in
	"Linux")
		source "${HOME}/.zshrc_linux"
		;;
	"Darwin")
		source "${HOME}/.zshrc_osx"
		;;
	*)
		source "${HOME}/.zshrc_generic"
		;;
esac

if [[ -f "${HOME}/.zshrc_local" ]] ; then
    source "${HOME}/.zshrc_local"
fi

# vim: set noexpandtab:
